plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "com.google.protobuf"
    id "com.vanniktech.android.junit.jacoco"
    id 'maven-publish'
}

android {
    compileSdk rootProject.targetSdkVersion

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += ['-module-name', "pufferdb-ktx"]
    }

    sourceSets {
        main.java.srcDirs += ['src/main/kotlin/', "$protobuf.generatedFilesBaseDir/main/javalite"]
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.startup:startup-runtime:$starupVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "com.google.protobuf:protobuf-lite:3.0.1"
}

junitJacoco {
    jacocoVersion = '0.8.5'
    excludes = ['**/cn/xihan/lib/pufferdb/proto/**']
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.4"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                from components.release
                groupId = "cn.xihan.lib"
                artifactId = "pufferdb-ktx"
                version = "1.1.1"
            }
        }
        repositories {
            maven{url 'file:///F:/Android//repository/'}
        }
    }
}
